#########################################
# Exercise 2.9 - Manufacturing Process
# From Deterministic Operations Research by David J. Rader Jr.
# AMPL model by Daniel Ware, University of Central Oklahoma
#########################################

########################
# SETS                 #
########################

# The set of all products produced
set PRODUCTS;

########################
# PARAMETERS           #
########################

# The revenue generated by selling one ounce of each product (in $ per oz)
param Revenue {PRODUCTS} >= 0;

# The maximum amount of each product that can be sold (in oz)
param MaxDemand {PRODUCTS} >= 0;

# The amount of each product (in oz) that can be made from one pound of raw material
param ProducedFromRaw {PRODUCTS} >= 0;

# The amount (in oz) of product j that can be produced from one ounce of product i
param AmountProduced {PRODUCTS, PRODUCTS} >= 0;

# The cost of converting one ounce of product i into product j
param ConversionCost {PRODUCTS, PRODUCTS} >= 0;

# The labor required to convert one ounce of product i into product j (in man-hours)
param ConversionLabor {PRODUCTS, PRODUCTS} >= 0;

# The cost of raw materials (in $ per lb)
param RawMaterialCost >= 0;

# The amount of time taken to process raw material (in man-hours per lb)
param RawMaterialLabor >= 0;

# The amount of raw material available (in lb)
param RawMaterialAvailable >= 0;

# The amount of labor available (in man-hours)
param LaborAvailable >= 0;

########################
# VARIABLES            #
########################

# the set of sellable products
var produce {PRODUCTS} >= 0;

# the set of the number of product i we convert to product j
var converted {PRODUCTS, PRODUCTS} >= 0;

# the raw resources we buy in lbs
var raw >= 0;

########################
# OBJECTIVE            #
########################

## The objective is to maximize the total profit from producing and selling product 1, 2, and 3
# the profit is calculated by sum of the revenue * the number of sellable products minus the cost of the raw materials plus
# the sum of converted products times the conversion costs
maximize TotalProfit: sum {i in PRODUCTS} Revenue[i] * produce[i] - 
				( sum {i in PRODUCTS, j in PRODUCTS} converted[i,j] * ConversionCost[i,j] ) - raw * RawMaterialCost;

#########################
# CONSTRAINTS           #
#########################

## The max amount of each product we can produce
subject to MaxDemandConstraint {i in PRODUCTS}: produce[i] <= MaxDemand[i];

## the max of raw materials we can purchase
subject to MaxMaterialConstraint: raw <= RawMaterialAvailable;

## the max available labor we can use
subject to LaborAvailableConstraint: raw * RawMaterialLabor + sum {i in PRODUCTS, j in PRODUCTS} ConversionLabor[i,j] * converted[i,j] <= LaborAvailable;

## the amount of product 1 we get is calculated here
subject to p1Constraint: ProducedFromRaw["Prod1"] * raw - converted["Prod1","Prod2"] - converted["Prod1","Prod3"] = produce["Prod1"];

## the amount of product 2 we get is calculated here
subject to p2Constraint: ProducedFromRaw["Prod2"] * raw + AmountProduced["Prod1", "Prod2"] * converted["Prod1","Prod2"] - converted["Prod2","Prod3"] = produce["Prod2"];

## the amount of product 3 we get is calculated here
subject to p3Constraint: AmountProduced["Prod1", "Prod3"] * converted["Prod1","Prod3"] + AmountProduced["Prod2", "Prod3"] * converted["Prod2","Prod3"] = produce["Prod3"];


